# Copyright 2020 The Fuchsia Authors
#
# Use of this source code is governed by a MIT-style
# license that can be found in the LICENSE file or at
# https://opensource.org/licenses/MIT

import("//build/cpp/library_headers.gni")
import("//build/toolchain/toolchain_environment.gni")

library_headers("headers") {
  visibility = [ "../*" ]

  headers = []
  public_deps = [
    "//zircon/system/ulib/hwreg:headers",

    # <lib/arch/internal/zbi-constants.h> has #include <fidl/zbi/data/asm/zbi.h>
    "//sdk/fidl/zbi:zbi_zither.asm",

    # <lib/arch/zbi-boot.h> has #include <lib/zbi-format/zbi.h>.
    # <lib/arch/internal/zbi-constants.h> has #include <lib/zbi-format/zbi.h>.
    "//sdk/lib/zbi-format",
  ]
}

static_library("arm64") {
  visibility = [ "../*" ]

  sources = [ "shadow-call-stack.S" ]
  deps = [ "..:headers" ]

  if (is_kernel) {
    sources += [
      "cache-ops.S",
      "cache.cc",
      "drop-el.S",
    ]

    deps += [ ":random" ]
  }
}

source_set("random") {
  visibility = [ ":*" ]

  deps = [
    "..:headers",
    "//zircon/system/ulib/hwreg",
  ]

  sources = [ "random.cc" ]

  configs += [ ":random.config" ]
}

config("random.config") {
  visibility = [ ":*" ]
  cflags = [ "-march=armv8.5-a+rng" ]
}
