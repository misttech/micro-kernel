# Copyright 2025 Mist Tecnologia LTDA. All rights reserved.
# Copyright 2023 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/kernel/standard.gni")
import("//build/toolchain/kernel/kernel_toolchain_suite.gni")

if (current_toolchain == default_toolchain) {
  foreach(cpu, standard_mk_cpus) {
    kernel_toolchain_suite("user.basic_$cpu") {
      cpu = cpu
      os = "mk"
      strip = "--strip-sections"
      environment = "user.basic"

      configs = [
        "//build/config/kernel:user",
        "//build/config/kernel:user-link",
        "//build/config:no-compiler-abi",
      ]
      source_deps = [ "//build/config/kernel:user_deps" ]

      # Since //build/config/zircon:user-executable uses BFD, we can't use ICF.
      # It's only supported by Gold and LLD.  But we can't use Gold for
      # executables because it doesn't have --no-dynamic-linker for static PIE.
      remove_executable_configs = [ "//build/config/kernel:default_icf" ]
      executable_configs = [ "//build/config/kernel:user-executable" ]

      tags = [
        "no-compiler-abi",
        "no-floating-point",
        "standalone",
      ]
      exclude_variant_tags = [ "needs-compiler-abi" ]
    }
  }
}
