# Copyright 2025 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# This file should only be included from BUILDCONFIG.gn while in the
# default toolchain context. It will define a variable called
# clang_toolchain_info that will be passed to other C++-related
# toolchains by variant_toolchain_suite().
#
if (current_toolchain == default_toolchain) {
  import("//build/config/clang/clang_prefix.gni")
  import("//build/config/python_interpreter.gni")

  # Invoke a script to generate a scope that contains information about
  # the Clang toolchain, such as its resource directory, and the location
  # of various runtime libraries such as libc++ or libclang_rt.<sanitizer>,
  # either in static or shared format.
  _clang_toolchain_info_generator =
      "//build/config/clang/generate_config_json.py"
  _clang_toolchain_info =
      exec_script(python_exe_src,
                  [
                    "-S",
                    rebase_path(_clang_toolchain_info_generator),
                    "--to-gn-scope",
                    "--clang-dir",
                    "${rebased_clang_prefix}/..",
                  ],
                  "json",
                  [
                    _clang_toolchain_info_generator,
                    "${clang_prefix}/../lib/runtime.json",
                  ])
} else {
  _clang_toolchain_info = {
  }
}

declare_args() {
  # A scope that contains information about the current Clang toolchain.
  # This should never be set as a build argument.
  clang_toolchain_info = _clang_toolchain_info
}
